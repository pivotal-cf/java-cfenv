buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.github.johnrengelman:shadow:8.1.1'
    }
}

plugins {
    id 'io.pivotal.cfenv.java-conventions'
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java-library'

description = 'java-cfenv-all, contains all java-cfenv modules in a convenient uberjar - to be used with CF Java Buildpack'

dependencies {
    api 'org.springframework.boot:spring-boot'
    api project(':java-cfenv-boot-pivotal-scs')
    api project(':java-cfenv-boot-pivotal-sso')
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

import com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer

shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()
    transform(PropertiesFileTransformer) {
        paths = ['META-INF/spring.factories']
        mergeStrategy = "append"
    }
    dependencies {
        exclude(dependency('org.springframework.boot::'))
        exclude(dependency('org.springframework::'))
    }
    relocate 'com.cedarsoftware.util.io', 'io.pivotal.cfenv.shaded.com.cedarsoftware.util.io'
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }

    repositories {
        maven {
            url = project.properties.getOrDefault('publicationRepository', "${System.getenv('HOME')}/.m2/repository")
        }
    }

}

assemble.dependsOn shadowJar
build.dependsOn shadowJar
publishShadowPublicationToMavenRepository.onlyIf { false }
publishShadowPublicationToMaven2Repository.onlyIf { false }