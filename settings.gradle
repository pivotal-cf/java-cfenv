pluginManagement {
    resolutionStrategy {
        eachPlugin {
            if (requested.id.namespace?.startsWith('org.asciidoctor.jvm')) {
                useVersion("4.0.3")
            }
        }
    }
    plugins {
        id "io.spring.nohttp" version "0.0.11"
        id 'org.asciidoctor.jvm.pdf'
        id 'org.asciidoctor.jvm.convert'
    }
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/release" }
        if (version =~ /((-M|-RC)[0-9]+|-SNAPSHOT)$/) {
            maven { url "https://repo.spring.io/milestone" }
        }
        if (version.endsWith('-SNAPSHOT')) {
            maven { url "https://repo.spring.io/snapshot" }
        }
    }
}

rootProject.name = 'java-cfenv'

include "java-cfenv"
include "java-cfenv-boot"
include "java-cfenv-tests-boot"
include "java-cfenv-boot-pivotal-scs"
include "java-cfenv-boot-pivotal-sso"
include "java-cfenv-all"
//include "java-cfenv-docs"
include "java-cfenv-jdbc"
include "java-cfenv-test-support"
